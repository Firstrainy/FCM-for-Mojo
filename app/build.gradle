apply plugin: 'com.android.application'

def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "moe.shizuku.fcmformojo"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode gitCommitCount
        versionName "0.2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "zh-rCN"
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.sign
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    compile.exclude group: 'com.android.support', module: 'support-v4'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:support-compat:26.0.0-beta2'
    implementation 'com.android.support:support-core-utils:26.0.0-beta2'
    implementation 'com.android.support:support-core-ui:26.0.0-beta2'
    implementation 'com.android.support:support-fragment:26.0.0-beta2'
    implementation 'com.android.support:recyclerview-v7:26.0.0-beta2'
    implementation 'moe.shizuku.support:preference:1.1'
    implementation 'moe.shizuku.support:support-utils:1.1'
    implementation 'moe.shizuku.support:privileged-api:23'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'
    implementation 'com.google.firebase:firebase-messaging:11.0.0'
}

def ksFile = rootProject.file('signing.properties')
def props = new Properties()
if (ksFile.canRead()) {
    props.load(new FileInputStream(ksFile))

    if (props != null) {
        android.signingConfigs.sign.storeFile file(props['KEYSTORE_FILE'])
        android.signingConfigs.sign.storePassword props['KEYSTORE_PASSWORD']
        android.signingConfigs.sign.keyAlias props['KEYSTORE_ALIAS']
        android.signingConfigs.sign.keyPassword props['KEYSTORE_ALIAS_PASSWORD']
    } else {
        println 'some entries in \'keystore.properties\' not found!'
    }
} else {
    println '\'keystore.properties\' not found!'
}

apply plugin: 'com.google.gms.google-services'